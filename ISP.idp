vocabulary V {
    type Fase isa int
    type Vak
    type VakNaam
    type VakGroepType constructed from {AVO,Verdere_Specialisatie,Opleiding,Bachelor_Verbredend,Specialisatie}
    type VakGroep 
    type Studiepunten isa int
    type Semester constructed from {Eerste,Tweede,Jaar}
    
    IsType(VakGroep,VakGroepType)
    Verplicht(Vak,VakGroep)
    InVakGroep(Vak,VakGroep)
    InFase(Vak,Fase)
    Geselecteerd(VakGroep)
    Geselecteerd(Vak,Fase)
    
    HeeftNaam(Vak):VakNaam
    partial MinAantalStudiepunten(VakGroep):Studiepunten
    partial MaxAantalStudiepunten(VakGroep):Studiepunten
    AantalStudiepunten(Vak):Studiepunten
    InSemester(Vak):Semester
}


theory T : V {
    //Er moet exact 1 opleiding geselecteerd worden.
    ?1vg[VakGroep] : IsType(vg,Opleiding) & Geselecteerd(vg).
    
    //Als er specialisaties bestaan, moet er exact 1 specialisatie geselecteerd worden.
    !vg[VakGroep] : IsType(vg,Specialisatie) => ?1vg1[VakGroep] : IsType(vg1,Specialisatie) & Geselecteerd(vg1).
    
    //Als er bachelor verbredende pakketten bestaan, moet er exact 1 pakket geselecteerd worden.
    !vg[VakGroep] : IsType(vg,Bachelor_Verbredend) => ?1vg1[VakGroep] : IsType(vg1,Bachelor_Verbredend) & Geselecteerd(vg1).
    
    //De vakgroepen AVO en verdere specialisatie kan je niet selecteren. 
	!vg[VakGroep] : IsType(vg,AVO) | IsType(vg,Verdere_Specialisatie) => ~Geselecteerd(vg).
    
    //Als je een vakgroep selecteerd, moeten alle verplichte vakken voor die vakgroep ook geselecteerd worden.
    !vg[VakGroep],v[Vak] : Geselecteerd(vg) & Verplicht(v,vg) => ?1f[Fase] : InFase(v,f) & Geselecteerd(v,f).
    
    //Het totaal aantal studiepunten van de geselecteerde vakken uit een specifieke vakgroep moet tussen het minimum en maximum aantal studiepunten liggen van diezelfde vakgroep.
    !vg[VakGroep] : MaxAantalStudiepunten(vg) > 0 => MinAantalStudiepunten(vg) =< sum{v[Vak],f[Fase],s[Studiepunten] : InVakGroep(v,vg) & Geselecteerd(v,f) & AantalStudiepunten(v)=s : s} =< MaxAantalStudiepunten(vg).
    
    //Het totaal aantal studiepunten van de geselecteerde vakken per fase moet tussen de 60 en 66 liggen.
    !f[Fase] : 60 =< sum{v[Vak],s[Studiepunten] : Geselecteerd(v,f) & AantalStudiepunten(v)=s : s} =< 66.
}

term O : V {
    sum{f[Fase], t[Studiepunten] : abs(sum{v[Vak], s[Studiepunten] : Geselecteerd(v,f) & AantalStudiepunten(v)=s & InSemester(v)=Eerste: s} - sum{v[Vak], s[Studiepunten] : Geselecteerd(v,f) & AantalStudiepunten(v)=s & InSemester(v)=Tweede: s})=t : t}
}

procedure main() {
    print(minimize(T,S,O)[0])
}

include "DomainTI.idp"