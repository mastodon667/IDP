vocabulary V {
    type Fase isa int
    type Vak
    type VakGroepType constructed from {AVO,Verdere_Specialisatie,Opleiding,Bachelor_Verbredend,Specialisatie,Keuze}
    type VakGroep 
    type Studiepunten isa int
    type AantalVakken isa int
    type Semester constructed from {Eerste,Tweede,Jaar}
    
    IsType(VakGroep,VakGroepType)
    Verplicht(Vak,VakGroep)
    InVakGroep(Vak,VakGroep)
    InFase(Vak,Fase)
    Verbonden(Vak,Vak)
    Geselecteerd(Vak,Fase)
	GeenInteresse(Vak)
    
    MinAantalStudiepunten(VakGroep):Studiepunten
    MaxAantalStudiepunten(VakGroep):Studiepunten
    AantalStudiepunten(Vak):Studiepunten
    GeselecteerdAantalStudiepuntenPerVakGroep(VakGroep):Studiepunten
    GeselecteerdAantalStudiepuntenPerSemester(Semester,Fase):Studiepunten
    GeselecteerdAantalStudiepuntenTotaal : Studiepunten
    GeselecteerdAantalVakken(VakGroep) : AantalVakken
    AantalVakkenInVakGroep(VakGroep) : AantalVakken
    VerschilPerFase(Fase) : Studiepunten
    AbsoluutVerschilPerFase(Fase) : Studiepunten
    InSemester(Vak):Semester
    Evenwicht : Studiepunten
}


theory T : V {
    //Alle verplichte vakken van een opleiding moeten geselecteerd worden.
    !vg[VakGroep],v[Vak] : IsType(vg,Opleiding) & Verplicht(v,vg) => ?1f[Fase] : InFase(v,f) & Geselecteerd(v,f). 
    
    //Als er specialisaties bestaan, moeten voor minstens 1 specialisatie alle verplichte vakken geselecteerd worden.
     !vg[VakGroep] : IsType(vg,Bachelor_Verbredend) => ?vg1[VakGroep] : IsType(vg,Bachelor_Verbredend) & GeselecteerdAantalVakken(vg1) >= AantalVakkenInVakGroep(vg1).
    
    !vg[VakGroep] : IsType(vg,Bachelor_Verbredend) => ?vg1[VakGroep] : IsType(vg1,Bachelor_Verbredend) & !v[Vak] : Verplicht(v,vg1) => ?1f[Fase] : InFase(v,f) & Geselecteerd(v,f).
    
    !vg1[VakGroep],vg2[VakGroep] : IsType(vg1,Bachelor_Verbredend) & IsType(vg2,Bachelor_Verbredend) & vg1 ~= vg2 & (?v1[Vak],f1[Fase] : InVakGroep(v1,vg1) & Geselecteerd(v1,f1)) => ~?v2[Vak],f2[Fase] : InVakGroep(v2,vg2) & Geselecteerd(v2,f2).
    
    !vg[VakGroep] : IsType(vg,Opleiding) => GeselecteerdAantalStudiepuntenPerVakGroep(vg) = sum{v[Vak], sp[Studiepunten],f[Fase] : Geselecteerd(v,f) & AantalStudiepunten(v)=sp : sp }.
    
    !vg[VakGroep] : IsType(vg,Verdere_Specialisatie) => GeselecteerdAantalStudiepuntenPerVakGroep(vg) = sum{v[Vak], sp[Studiepunten],f[Fase], vg1[VakGroep] : (InVakGroep(v,vg) | (InVakGroep(v,vg1) & IsType(vg1,Specialisatie))) & Geselecteerd(v,f) & AantalStudiepunten(v)=sp : sp }.
    
    !vg[VakGroep] : IsType(vg,AVO) => GeselecteerdAantalStudiepuntenPerVakGroep(vg) = sum{v[Vak], sp[Studiepunten],f[Fase] : InVakGroep(v,vg) & Geselecteerd(v,f) & AantalStudiepunten(v)=sp : sp }.
    
    !vg[VakGroep] : IsType(vg,Specialisatie) | IsType(vg,Bachelor_Verbredend) => GeselecteerdAantalStudiepuntenPerVakGroep(vg) = 0.
    
    //Het totaal aantal studiepunten van de geselecteerde vakken uit een specifieke vakgroep moet tussen het minimum en maximum aantal studiepunten liggen van diezelfde vakgroep.
    !vg[VakGroep] : MaxAantalStudiepunten(vg) > 0 => MinAantalStudiepunten(vg) =< GeselecteerdAantalStudiepuntenPerVakGroep(vg) =< MaxAantalStudiepunten(vg).
    
    !v1[Vak],v2[Vak] : Verbonden(v1,v2) => ~?f1[Fase],f2[Fase] : Geselecteerd(v1,f1) & Geselecteerd(v2,f2).
    
    //Het totaal aantal studiepunten van de geselecteerde vakken per fase moet tussen de 60 en 66 liggen.
    !f[Fase] : 60 =< sum{v[Vak],s[Studiepunten] : Geselecteerd(v,f) & AantalStudiepunten(v)=s : s} =< 66.
   	
    !s[Semester],f[Fase] : GeselecteerdAantalStudiepuntenPerSemester(s,f) = sum{v[Vak],sp[Studiepunten] : InSemester(v)=s & Geselecteerd(v,f) & AantalStudiepunten(v)=sp : sp}.
   
    !v[Vak] : GeenInteresse(v) => ~?f[Fase] : Geselecteerd(v,f).
    
    GeselecteerdAantalStudiepuntenTotaal = sum{v s f : Geselecteerd(v,f) & AantalStudiepunten(v) = s : s}.
    
    !f[Fase] : VerschilPerFase(f) = sum{ sp[Studiepunten] : GeselecteerdAantalStudiepuntenPerSemester(Eerste,f)=sp : sp } - sum{ sp[Studiepunten] : GeselecteerdAantalStudiepuntenPerSemester(Tweede,f)=sp : sp }.
    
    !vg[VakGroep] : IsType(vg,Bachelor_Verbredend) => GeselecteerdAantalVakken(vg) = #{v f vg1 : Geselecteerd(v,f) & (InVakGroep(v,vg) | (InVakGroep(v,vg1) & IsType(vg1,Verdere_Specialisatie)))}.
    
    !vg[VakGroep] : IsType(vg,Bachelor_Verbredend) => AantalVakkenInVakGroep(vg) = #{v : InVakGroep(v,vg)}.
   	
    !f[Fase] : abs(VerschilPerFase(f)) = AbsoluutVerschilPerFase(f).
    
    Evenwicht = sum{f sp : AbsoluutVerschilPerFase(f) = sp : sp}.
}

term T1 : V {
    Evenwicht
}

term T2 : V {
    GeselecteerdAantalStudiepuntenTotaal
}

vocabulary V_out {
    extern V::Geselecteerd/2
    extern V::GeenInteresse/1
    //extern V::AantalStudiepunten/1:1
    //extern V::GeselecteerdAantalStudiepuntenPerVakGroep/1:1
}


procedure main() {
    //stdoptions.nbmodels = 1
    //printmodels(modelexpand(T,S))
    //sols = modelexpand(T,S)
    //print(#sols.." solutions found.")
    //printmodels(sols)
    //print(minimize(T,S,T2)[1])
    //models = modelexpand(T,S)
    //explainunsat(T,S,V_out)
    //print(query(Q,models[1]))
    //print(sat(T,S))
    //unsatstructure(T,S)
    //print(unsatstructure(T,S))
    //print(unsatstructure(T,S,V_out))
    print(onemodel(T,S))
}

include "DomainCW.idp"
